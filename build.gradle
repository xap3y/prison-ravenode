// Gradle template for a paper plugin from: https://github.com/xap3y/templateBukkitGradle
plugins {
    id 'com.github.johnrengelman.shadow' version '8.1.1'
    id 'java'
    id "net.kyori.blossom" version "1.3.1"
    id 'io.freefair.lombok' version '8.7.1'
}

group = 'eu.xap3y'
version = '1.0'
def baseCoordinates = "Prison"

// OPTIONAL
blossom {
    replaceToken("@VERSION@", version)
    replaceToken("@MAIN@", "${group}.${baseCoordinates.toLowerCase()}")
}

repositories {
    mavenCentral()
    maven {
        name = "papermc-repo"
        url = "https://repo.papermc.io/repository/maven-public/"
    }
    maven {
        name = "sonatype"
        url = "https://oss.sonatype.org/content/groups/public/"
    }
    maven { url "https://jitpack.io" }
    maven {
        url = 'https://repo.extendedclip.com/content/repositories/placeholderapi/'
    }
}

dependencies {
    compileOnly "io.papermc.paper:paper-api:1.20.4-R0.1-SNAPSHOT"
    implementation 'eu.xap3y:xagui:1.2-beta.1'
    implementation 'com.github.xap3y:xalib:ec589911f642398833b467cab0cbc032d7f7162a'
    implementation 'org.incendo:cloud-annotations:2.0.0-rc.2'
    implementation 'org.incendo:cloud-paper:2.0.0-beta.9'
    implementation 'com.github.xap3y:skullcreator:ccdc446'
    implementation 'fr.mrmicky:fastboard:2.1.3'
    implementation 'com.github.fierioziy.particlenativeapi:ParticleNativeAPI-core:4.3.0'
    implementation 'com.google.code.gson:gson:2.11.0'
    compileOnly 'me.clip:placeholderapi:2.11.6'
    compileOnly 'org.apache.logging.log4j:log4j-core:2.23.1'
    compileOnly 'me.clip:placeholderapi:2.11.6'

}

def targetJavaVersion = 17
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'

    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release.set(targetJavaVersion)
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}

build {
    dependsOn(shadowJar)
}

tasks.jar {
    enabled = false
}

tasks {
    shadowJar {
        archiveClassifier.set('')
        archiveBaseName.set(baseCoordinates)
        relocate 'eu.xap3y.xagui', "${group}.${baseCoordinates.toLowerCase()}.util.xagui"
        relocate 'eu.xap3y.xalib', "${group}.${baseCoordinates.toLowerCase()}.util.xalib"
        relocate 'eu.xap3y.skullcreator', "${group}.${baseCoordinates.toLowerCase()}.util.skull"
        relocate 'fr.mrmicky.fastboard', "${group}.${baseCoordinates.toLowerCase()}.util.fastboard"
        relocate 'com.github.fierioziy.particlenativeapi', "${group}.${baseCoordinates.toLowerCase()}.util.particlenativeapi"
    }
}